<script type="statebus"># -*- mode: coffee -*-

bus.honk = false

include '/code/considerit/shared.coffee', -> css_reset()
include '/code/considerit/types.coffee'
include '/code/considerit/slidergrams.coffee'

include '/code/earl.coffee', -> 
  Earl.start_work
    history_aware_links: true

include '/code/considerit/fickle.coffee', -> 
  fickle.init (vars) -> 
    gutter: 50 
    spacing: 18
    indent: 50
    max_width: 900

##############
# Colors
##############

focus_blue = '#2478CC'
transparent_focus_blue = (opacity) -> 
  "rgba(36, 120, 204, #{opacity})"
feedback_orange = '#F19135'
logo_red = "#B03A44"
light_gray = '#afafaf'
light_blue = '#F6F7F9'


slider_color = '#999'
index_bg = '#F5F5F5' #'#F4F6F8'
attention_magenta = '#FF00A4'
open_point_color = 'black' #focus_blue #'#A13A9E' #
list_header_color =  focus_blue #'#D18042'

fontsize = { normal: 16 }

list_styles = 
  title: 
    fontWeight: 700
    fontSize: 22

  heading1: 
    textTransform: 'uppercase'
    color: list_header_color

    fontSize: 32
    fontWeight: 900

    # fontSize: 24
    # fontWeight: 400
    # fontFamily: 'Architects Daughter W00'

  heading2: 
    fontSize: 22
    fontWeight: 500

root = fetch("/point/proto_root")


#########
# Body: main content area

dom.BODY = ->  
  fetch 'include'

  DIV
    id: 'main-content' #used in sizewhenrendered
    style:
      fontFamily: 'Avenir Next W01' #, Avenir Next, Avenir, Helvetica, sans-serif'
      fontSize: fontsize.normal
      position: 'relative'
      #backgroundColor: '#eaeaea'

    onClick: -> 
      close_children()

    LINK 
      href: 'http://fast.fonts.net/cssapi/9cd0e917-fcb5-4c2e-8e1c-e6789e1fd413.css'
      rel: 'stylesheet'
      type: 'text/css'

    # DIV 
    #   style: 
    #     position: 'relative'
    #     display: 'inline-block'
    #     marginRight: 20
    #   LOGIN()

    DIV 
      ref: root.key
      style: 
        position: 'relative'


      POINT 
        key: root.key 
        point: root
        depth: 0

    if TOOLTIP?
      TOOLTIP key: 'tooltip'



dom.POINT = -> 
  pnt = fetch @props.point

  depth = @props.depth 
  max_depth = max_depth_of_open_point()

  unfocused = get_open_children(pnt).length > 0

  avatar_offset = point_icon_size(pnt) + fickle.spacing

  slidergrams = (pnt.sliders or [])

  w = text_width(pnt, 0) + opinion_width(pnt, 0) + fickle.gutter

  if !@local.collapse_description? && !@loading()
    s = sizeWhenRendered "<DIV class='embedded_html'>#{pnt.description}</div>",
        fontSize: 21 

    if s.height >= 300      
      @local.description_collapsed = @local.collapse_description = true
    else 
      @local.collapse_description = false
    save @local

  DIV 
    style: 
      position: 'relative'
      marginBottom: 20
      backgroundColor: 'white'
      top: if depth > 0 then -18 #-1 - fickle.spacing
      width: w + fickle.gutter * (if depth == 0 then 2 else 1) + fickle.indent + 1
      padding: "0 #{fickle.gutter}px 0 #{(if depth == 0 then fickle.gutter else 0)}px"
      cursor: if depth < max_depth then 'pointer' else 'default'
      #boxShadow: "8px 0px 4px -6px rgba(0,0,0,.25)"
      borderRadius: '0 8px 8px 0'

    onClick: (e) => 
      close_children pnt
      e.stopPropagation()


    # Heading 

    DIV 
      style:
        marginTop: 18
        borderRadius: '8px 8px 0 0'

      POINT_ICON 
        pnt: pnt 
        left: -28
        top: 10
        style: 
          color: open_point_color

      # Title
      SPAN # span is for parity with subpoint
        style: extend {}, list_styles.title,
          paddingBottom: 0
          cursor: 'pointer'
          paddingRight: 20
          width: w
          borderBottom: '1px solid transparent' # for parity with subpoint
          color: open_point_color

        onMouseEnter: => @local.hover = pnt.key; save(@local)
        onMouseLeave: => @local.hover = false; save(@local)
        onClick: (e) => 
          toggle_point pnt, depth
          e.stopPropagation()

        pnt.summary

      DIV 
        style: 
          fontSize: 16
          color: light_gray
          fontStyle: 'italic'

        if pnt.creator
          SPAN 
            style: {}

            "by #{fetch(pnt.creator)?.name or 'Anonymous'}"




    # the rest...
    DIV 
      style: 
        paddingLeft: fickle.indent
        borderLeft: if depth > 0 then "1px dashed #{focus_blue}"
        borderBottomLeftRadius: 16

      # Description
      if pnt.description
        collapse = !@local.collapse_description? || \
                    @local.description_collapsed 

        DIV 
          style: 
            width: w
            paddingTop: 10
            marginTop: 10

          DIV 
            className: 'embedded_html'
            ref: 'description'
            style:
              fontSize: 18
              maxHeight: if collapse then 300
              overflow: if collapse then 'hidden'

            dangerouslySetInnerHTML:
              __html: pnt.description

          if @local.collapse_description
            DIV 
              style:
                backgroundColor: '#f6f7f9'
                cursor: 'pointer'
                #textAlign: 'center'
                padding: 8
                paddingLeft: 0
                color: focus_blue
                fontSize: 18
                display: 'inline-block'
                marginTop: 4

              onClick: => 
                @local.description_collapsed = !@local.description_collapsed
                save @local

              if @local.description_collapsed
                SPAN 
                  style: 
                    textDecoration: 'underline'
                  'Expand full text'
              else 
                'Hide full text'


      # Slidergrams
      if slidergrams.length > 0
        DIV
          style: 
            paddingTop: 40
            marginTop: 20
            marginBottom: 80

          for sldr in slidergrams
            histoheight = Math.min 150, 4 * (sldr.opinions or []).length + 20 

            SLIDERGRAM
              width: w
              height: histoheight
              sldr: sldr
              base_thickness: 2
              show_labels: true


      # Sublists
      DIV 
        style: {}

        for [type, children] in organize_children_by_type(pnt)
          LIST
            parent: pnt
            type: type
            points: children
            depth: @props.depth
            unfocused: unfocused

        # NEW_LIST
        #   suggested_by: pnt



dom.LIST = -> 
  type = fetch @props.type  
  return SPAN null if @loading()

  if type.sliders?.length > 0
    labels = resolve type.sliders[0], 'labels'
  else 
    labels = null 

  # if @local.editing 
  #   return EditList 
  #     type: type.key 
  #     suggested_by: @props.parent
  #     done: => @local.editing = false; save @local 

  # @draw_list_menu = => 

  #   li_style = 
  #     padding: "8px 16px"
  #     textAlign: 'right'
  #     position: 'relative'
  #     display: 'block'

  #   SPAN 
  #     style:
  #       marginLeft: 10
  #       cursor: 'pointer'
  #       position: 'relative'

  #     onClick: => @local.list_menu = !@local.list_menu; save @local

  #     String.fromCodePoint parseInt('25BE',16) # down triangle

  #     if @local.list_menu
        
  #       UL 
  #         style: 
  #           position: 'absolute'
  #           backgroundColor: '#F7F7F7'
  #           borderRadius: 16
  #           border: "1px solid #DADADA"
  #           right: -15
  #           fontSize: fontsize.normal
  #           zIndex: 1

  #         LI 
  #           style: li_style
  #           onClick: => @local.editing = true; save @local      
  #           'edit'

  #         LI 
  #           style: li_style
  #           onClick: => delete_point type

  #           'delete'

  collapse_list_length = 5

  if !@local.collapsed?
    @local.collapsed = @props.points?.length > collapse_list_length + 1
    @local.toggled = false 

  points = @props.points or []
  total_points = points.length

  if @local.collapsed
    points = points.slice(0,collapse_list_length)

  DIV 
    style: 
      marginTop: 20
      marginBottom: 45
      

    DIV 
      style: 
        position: 'relative'
      onMouseEnter: => @local.hover_heading = true; save @local 
      onMouseLeave: => 
        @local.hover_heading = @local.list_menu = false; save @local

      DIV 
        style: extend {}, list_styles.heading1,
          display: 'inline-block'
          marginBottom: 32
          width: text_width(type, 0)

        resolve_type(type, 'category') or 'Points'

        #@draw_list_menu()

      DIV 
        style: 
          width: opinion_width(type, 0)
          display: 'inline-block'
          position: 'relative'
          left: fickle.gutter
        DIV 
          style: extend {}, list_styles.heading1,
            textAlign: 'center'

          resolve_type(type, 'opinion_heading') or 'Opinions'

        if labels
          DIV 
            style: 
              fontWeight: 300
              fontSize: 18
              marginTop: -8

            SPAN 
              style: 
                position: 'absolute'
                left: 0

              labels[0]

            SPAN 
              style: 
                position: 'absolute'
                right: 0
              labels[1] 


    DIV
      style: 
        marginTop: 0

      for pnt in (points or [])
        if is_point_open pnt, @props.depth + 1
          POINT 
            key: pnt.key or pnt 
            point: pnt
            depth: @props.depth + 1
        else
          DIV 
            style: {}
            SUBPOINT 
              key: pnt.key or pnt 
              point: pnt
              depth: @props.depth

      DIV 
        style: {}

        if !@local.collapsed
          NEW_POINT
            type: type
            parent: @props.parent
            depth: @props.depth + 1

        else 
          DIV 
            style:
              backgroundColor: '#f6f7f9'
              cursor: 'pointer'
              #textAlign: 'center'
              padding: 8
              marginLeft: -8
              color: focus_blue
              fontSize: 18
              display: 'inline-block'
              marginTop: 4

            onClick: => 
              @local.collapsed = false
              save @local


            "Show #{total_points - collapse_list_length} more"
            SPAN 
              style: 
                color: '#777'
              " or "
            "add a new one"

dom.SUBPOINT = -> 
  pnt = fetch @props.point

  depth = @props.depth

  icon_size = point_icon_size pnt

  separator = SPAN 
    # dangerouslySetInnerHTML: { __html: '&bull;'}
    style: 
      padding: '0 8px'


  DIV 
    id: "subpoint-#{pnt.key}"
    style:
      paddingBottom: 20
      display: 'block'
      position: 'relative'
      minHeight: icon_size + 20
      #marginLeft: 0 #icon_size + fickle.spacing

    POINT_ICON 
      pnt: pnt 
      collapsed: true
      top: 10


    DIV
      style: 
        width: text_width(pnt, 0) #- fickle.spacing 
        display: 'inline-block'
        verticalAlign: 'top'



      SPAN 
        style: extend {}, list_styles.heading2,
          borderBottom: "1px solid \
                         #{if @local.hover then 'black' else 'transparent'}"
          cursor: 'pointer'

        onMouseEnter: => @local.hover = true; save(@local)
        onMouseLeave: => @local.hover = false; save(@local)

        onClick: (e) => 
          toggle_point pnt, depth
          e.stopPropagation()

        pnt.summary

        if pnt.description?.length > 0
          SPAN 
            dangerouslySetInnerHTML: { __html: '&hellip;'}
            style: 
              color: light_gray

      DIV 
        style: 
          fontSize: 16
          color: light_gray
          fontStyle: 'italic'

        if pnt.creator
          SPAN 
            style: {}

            "by #{fetch(pnt.creator)?.name or 'Anonymous'}"

            separator

        if pnt.children?.length > 0
          SPAN 
            style: {}
            # SPAN 
            #   dangerouslySetInnerHTML: { __html: '&bull;'}
            #   style: 
            #     paddingRight: 4
            "#{pnt.children?.length} subpoint#{if pnt.children.length != 1 then 's' else ''}"


        # SPAN 
        #   style: 
        #     cursor: 'pointer'
        #   onClick: => delete_point(pnt) # TODO: prompt for confirmation

        #   'delete'

    DIV
      style: 
        display: 'inline-block'
        position: 'relative'
        left: fickle.gutter

      for sldr in (pnt.sliders or [])
        SLIDERGRAM
          width: opinion_width(pnt, 0)
          height: 50
          sldr: sldr


dom.NEW_POINT = -> 
  type = @props.type.key or @props.type
  you = your_key()
  w = 500

  icon_size = point_icon_size type

  DIV 
    style:
      position: 'relative'
      marginLeft: icon_size + fickle.spacing
      minHeight: icon_size + 20

    POINT_ICON
      user: you 
      pnt: type

    if !@local.editing
      SPAN 
        style: extend {}, list_styles.heading2,
          #marginLeft: 68
          color: focus_blue
          cursor: 'pointer'
          borderBottom: "1px solid #{focus_blue}"

        onClick: (e) => 
          @local.editing = true; save(@local)
        
        "Add new point"

    else 

      DIV 
        style: 
          marginLeft: -9
          display: 'inline-block'
          width: w
        TEXTAREA 
          key: 'summary'
          ref: 'summary'
          pattern:'^.{3,}'
          placeholder: "Summarize your point"
          required:'required'
          style: extend {}, list_styles.heading2,
            width: w
            border: "1px solid #eaeaea"
            outline: 'none'
            padding: '6px 8px'
            resize: 'none'

        GROWING_TEXTAREA
          key: 'description'
          ref: 'description'
          placeholder: "Add details here (optional)"

          style: 
            fontSize: fontsize.normal
            padding: '6px 8px'
            marginTop: 8
            width: '100%'
            minHeight: 40
            border: '1px solid #eaeaea'   
            outline: 'none'       

        DIV 
          style: 
            marginTop: 8

          SPAN 
            style: 
              backgroundColor: focus_blue
              color: 'white'
              cursor: 'pointer'
              borderRadius: 16
              padding: '4px 24px'
              display: 'inline-block'
              marginLeft: 12
              float: 'right'

            onClick: => 
              create_point
                parent: @props.parent
                type: type
                summary: @refs.summary.getDOMNode().value
                description: @refs.description.getDOMNode().value 

              @local.editing = false 
              save @local

            'Done'

          SPAN 
            style: 
              color: '#888'
              cursor: 'pointer'
              float: 'right'
              padding: '4px 0px'
            onClick: => @local.editing = null; save(@local)
            'cancel'

          DIV style: clear: 'both'



point_icon_size = (pnt) -> 
  pnt = fetch pnt 
  icon = false && resolve_type pnt, 'icon'

  if icon
    40
  else 
    10

dom.POINT_ICON = ->
  pnt = fetch @props.pnt 
  icon = false && resolve_type pnt, 'icon'

  if @props.user 
    user = @props.user 
  else 
    user = pnt.creator 

  size = point_icon_size pnt

  style = extend {}, @props.style, 
    position: 'absolute'
    left: @props.left or -size - fickle.spacing
    top: @props.top or 4
    verticalAlign: 'top'

  size_sty = 
    height: size
    width: size

  #return SPAN null if !icon

  SPAN 
    style: style 

    if icon == 'author'
      AVATAR
        user: user 
        style: size_sty

    else if icon 
      IMG 
        src: icon 
        style: size_sty
    else
      SPAN 
        style: cssTriangle (if @props.collapsed then 'right' else 'bottom'), \
                           (@props.style?.color or 'black'), size, size,
          width: size
          height: size
          display: 'inline-block'

      # SPAN 
      #   dangerouslySetInnerHTML:
      #     # __html: if pnt.parent && !is_point_open(pnt) then '&bull;'
      #     __html: if pnt.parent then '&bull;'


default_open_depth = 2
is_point_open = (pnt, depth) -> 

  o = fetch('open_points')
  if !o[(pnt.key or pnt)]?
    depth <= default_open_depth
  else 
    o[(pnt.key or pnt)] != -1

toggle_point = (pnt, depth) -> 
  open = fetch('open_points')
  if open[pnt.key]?
    open[pnt.key] = -1
  else
    open[pnt.key] = depth or 0
  save open

get_open_children = (pnt) -> 
  open = fetch('open_points')
  pnt = fetch pnt 

  open_children = []
  for k,v of open 
    if v != -1 && k != 'key'
      ancestor = fetch k 
      while ancestor.parent && ancestor = fetch(ancestor.parent)
        if ancestor.key == pnt.key
          open_children.push k 
          continue 

  open_children 


close_children = (pnt) -> 
  open = fetch('open_points')

  if pnt 
    pnt = fetch pnt 
    open_children = get_open_children pnt 
  else 
    open_children = (k for k,v of open when k != 'key')

  if open_children.length > 0 
    for k in open_children
      open[k] = -1
    save open

close_points = (new_max) -> 
  open = fetch('open_points')

  to_close = []
  for k,v of open when v != -1
    if k != 'key' && v > new_max 
      to_close.push k 

  if to_close.length > 0 
    for k in to_close
      open[k] = -1
    save open

  open 

max_depth_of_open_point = -> 
  open = fetch('open_points')
  max = 0
  for k,v of open when v != -1
    if k != 'key' && v > max 
      max = v
  max 


grid_unit = (pnt, depth) -> 
  depth ||= 0

  w = fickle.window_width - 3 * fickle.gutter

  w = Math.min w, fickle.max_width

  if depth > 0
    depth_cost = point_icon_size(pnt) + fickle.spacing
    w -= depth * depth_cost

  w = Math.floor(w / 3) * 3 # round down to nearest 100 so we don't 
                                # re-layout all the time
  w / 8

text_width = (pnt, depth) -> 
  5 * grid_unit(pnt, depth) - fickle.gutter

opinion_width = (pnt, depth) -> 3 * grid_unit(pnt, depth)



# Earl.start_work
#   history_aware_links: true
#   root: location.pathname  # this isn't quite right...

# Earl.load_page root.key.substring(1, root.key.length)

#</script>

<script>
  var OFFLINE_DEV = true 
  var LOCAL_STATEBUS = true

  // The statebus server we will connect to. Read by statebus_client.
  if (window.location.hostname == 'localhost')
    statebus_server = 'http://localhost:9375'
  else 
    statebus_server = 'http://considerit.us:9375'
    
  
  if (LOCAL_STATEBUS)
    var statebus_client = "/considerit-local/node_modules/statebus/client.js"
  else 
    var statebus_client = "https://stateb.us/client.js?v3" 
    
  // External scripts to load
  var scripts_to_load = [ 
    statebus_client
  ]

  for (script in scripts_to_load)
    document.write("<script id=js-"+ script +  " src='" + scripts_to_load[script] + "' charset='utf-8'></script\>" )

</script>