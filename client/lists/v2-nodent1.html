<script type="statebus"># -*- mode: coffee -*-

bus.honk = false
bus.dev_with_single_client = false


include '/code/considerit/shared.coffee', -> css_reset()
include '/code/considerit/types.coffee'
include '/code/considerit/slidergrams.coffee'

include '/code/earl.coffee', -> 
  Earl.start_work
    history_aware_links: true

include '/code/considerit/fickle.coffee', -> 
  fickle.init (vars) -> 
    gutter: 50 
    max_text_width: 650
    indent: 0
    collapsed_histo_height: 40
    bubble_padding: 50

dom.BODY = -> 

  fetch 'include'

  loc = fetch 'location'
  slug =  if !loc.url || loc.url == '/' 
            'proto_root' 
          else
            loc.url.replace(/\/$/, '')
  root = "/point/#{slug}"

  DIV
    id: 'main-content' #used in sizewhenrendered
    style:
      fontFamily: avenir
      fontSize: 16
      position: 'relative'
      #backgroundColor: '#eaeaea'

    onClick: -> 
      close_children()

    project_fonts() if project_fonts?
    DIV 
      style: 
        position: 'relative'
        padding: '60px 44px 80px 100px'


      POINT 
        key: root
        point: root
        depth: 0

    if TOOLTIP?
      TOOLTIP key: 'tooltip'

dom.POINT = -> 
  collapsed = @props.collapsed

  pnt = fetch @props.point
  depth = @props.depth 

  w = working_width(depth) + fickle.gutter + 2 * fickle.bubble_padding

  histoheight = if collapsed 
                  fickle.collapsed_histo_height 
                else 
                  Math.min(300, Math.max(50, 3 * pnt.sliders?[0].opinions.length))

  minimize = collapsed && get_open_children(pnt.parent).length > 0
  DIV 
    style: 

      opacity: if minimize then .3
      pointerEvents: if minimize then 'none'

      position: 'relative'
      left: -fickle.bubble_padding # for the padding in the point bubble
      marginBottom: 9
      width: w

      backgroundColor: if depth > 0 && !collapsed && get_open_children(pnt).length == 0 then light_blue
      padding: "10px #{fickle.bubble_padding}px"
      borderRadius: 16
      boxShadow: if depth > 0 && !collapsed then "0 1px 2px 0px rgba(0,0,0,.4)"


    onClick: (e) =>
      close_children pnt
      e.stopPropagation()

    if pnt.parent && depth == 0 
      NAVIGATION
        pnt: pnt 

    # point description
    DIV 
      style:
        position: 'relative'
        display: 'inline-block'
        verticalAlign: 'top'
        width: if pnt.sliders?.length > 0 then text_width(pnt, depth) else w

      POINT_DESCRIPTION
        pnt: pnt
        depth: depth
        collapsed: collapsed
        min_height: if !collapsed && pnt.sliders?.length > 0 
                      histoheight + 15 
                    else 
                      fickle.collapsed_histo_height

    # slidergrams
    DIV
      style: 
        display: 'inline-block'
        position: 'relative'
        paddingLeft: fickle.gutter
        verticalAlign: if !collapsed then 'bottom'
        bottom: if !collapsed then 10

      for sldr in (pnt.sliders or [])
        SLIDERGRAM
          width: opinion_width(pnt, depth)
          height: histoheight
          sldr: sldr
          show_labels: true

    # sublists
    if !collapsed
      DIV 
        style: 
          position: 'relative'
          marginTop: 35

        for [type, children] in organize_children_by_type(pnt)
          LIST
            key: type.key or type
            parent: pnt
            type: type
            points: children
            depth: @props.depth
            unfocused: false

dom.LIST = -> 
  type = fetch @props.type  
  return SPAN null if @loading()

  collapse_list_length = 5

  if !@local.collapsed?
    @local.collapsed = @props.points?.length > collapse_list_length + 1

  points = @props.points or []
  total_points = points.length

  if @local.collapsed
    points = points.slice(0,collapse_list_length)

  DIV 
    style: 
      marginTop: 20
      marginBottom: 45
      position: 'relative'
      


    DIV 
      style: extend {}, list_styles.heading1,
        fontSize: list_styles.heading1.fontSize - @props.depth * 2
        marginBottom: 8
        position: 'relative'

      resolve_type(type, 'category') or 'Points'

    DIV
      style: 
        marginTop: 0
        position: 'relative'
        left: fickle.indent

      for pnt in (points)
        POINT 
          key: pnt.key or pnt 
          point: pnt
          depth: @props.depth + 1
          collapsed: !is_point_open(pnt)


      if !@local.collapsed
        NEW_POINT
          type: type
          parent: @props.parent
          depth: @props.depth + 1

      else 
        DIV 
          style:
            backgroundColor: '#f6f7f9'
            cursor: 'pointer'
            padding: 8
            marginLeft: -8
            color: focus_blue
            fontSize: 18
            display: 'inline-block'
            marginTop: 4

          onClick: => 
            @local.collapsed = false
            save @local


          "Show #{total_points - collapse_list_length} more"
          SPAN 
            style: 
              color: '#777'
            " or "
          "add a new one"

dom.POINT_DESCRIPTION = -> 
  collapsed = @props.collapsed
  pnt = @props.pnt 
  min_height = @props.min_height 
  depth = @props.depth 

  point_avatar_size = 40 - 5 * depth


  if !@local.collapse_description? && !@loading() && !collapsed
    s = sizeWhenRendered "<DIV class='embedded_html'>#{pnt.description}</div>",
        fontSize: 21 

    if s.height >= 300      
      @local.description_collapsed = @local.collapse_description = true
    else 
      @local.collapse_description = false
    save @local


  DIV
    style:
      border: '1px transparent' #parity with text area
      boxSizing: 'border-box'
      minHeight: min_height
      wordWrap: 'break-word'
      position: 'relative' 
                    # positioning is to force Chrome to confine text
                    # selections to within the element. 
                    # http://stackoverflow.com/questions/14017818

    # AVATAR
    #   onClick: if !collapsed then (e) =>
    #     toggle_point pnt, depth
    #     e.stopPropagation()      
    #   user: pnt.creator
    #   hide_tooltip: true      
    #   style: 
    #     width: point_avatar_size
    #     height: point_avatar_size
    #     left: -point_avatar_size - fickle.bubble_padding
    #     position: 'absolute'
    #     top: 16
    #     verticalAlign: 'top'
    #     display: if pnt.treat_authorless then 'none'
    #     cursor: 'pointer'


    # Summary
    SPAN 
      style: 
        fontSize: 27 - depth * 3
        fontWeight: 500
        cursor: if depth > 0 then 'pointer'
        borderBottom: "1px solid \
                       #{if @local.hover && collapsed then 'black' else 'transparent'}"
      onClick: (e) =>
        @local.hover = false
        toggle_point pnt, depth
        e.stopPropagation()
      onMouseEnter: if collapsed then => @local.hover = true; save(@local)
      onMouseLeave: if collapsed then => @local.hover = false; save(@local)


      pnt.summary
      if pnt.description?.length > 0 && collapsed
        SPAN 
          dangerouslySetInnerHTML: { __html: '&hellip;'}
          style: 
            color: light_gray

    # Meta data
    DIV 
      style: 
        fontSize: Math.min(16, 27 - depth * 3 - 2)
        color: light_gray
        fontStyle: 'italic'

      SPAN 
        style:
          paddingRight: 16

        prettyDate pnt.created_at

        if pnt.creator
          SPAN 
            style: {}
            " by #{fetch(pnt.creator)?.name or 'Anonymous'}"


      if pnt.children?.length > 0 && collapsed
        SPAN null, 
          SPAN 
            dangerouslySetInnerHTML: { __html: '&bull;'}
            style: 
              paddingRight: 4
          "#{pnt.children?.length}"

      if !collapsed && depth > 0 
        IMG 
          src: '/images/expand.png'
          style:
            height: 15
            width: 15
            position: 'relative'
            top: 2
            cursor: 'pointer'
            opacity: .3              
          onClick: => 
            close_children()
            Earl.load_page pnt.key.substring(7)
            scrollTo(0,0)


    # Description
    if pnt.description && !collapsed
      collapse = !@local.collapse_description? || \
                  @local.description_collapsed 

      DIV 
        style: 
          paddingTop: 10
          marginTop: 10

        DIV 
          style:
            fontSize: 18
            maxHeight: if collapse then 300
            overflow: if collapse then 'hidden'

          RENDER_HTML
            html: pnt.description

        if @local.collapse_description
          DIV 
            style:
              backgroundColor: '#f6f7f9'
              cursor: 'pointer'
              padding: 8
              paddingLeft: 0
              color: focus_blue
              fontSize: 18
              marginTop: 4

            onClick: (e) => 
              @local.description_collapsed = !@local.description_collapsed
              save @local
              e.stopPropagation()

            if @local.description_collapsed
              SPAN 
                style: 
                  textDecoration: 'underline'
                'Expand full text'
            else 
              'Hide full text'

dom.NAVIGATION = -> 
  parent = fetch(@props.pnt.parent)
  DIV 
    style:
      position: 'absolute'
      cursor: 'pointer'
      left: -78
      top: -46
      fontSize: 18
      color: light_gray
    onClick: => 
      Earl.load_page parent.key.substring(7)
      scrollTo(0,0)

    SPAN 
      style: 
        paddingRight: 8
      "<"
    
    "#{parent.summary.substring(0,30)}#{if parent.summary.length > 30 then '...' else ''}"


working_width = (depth) -> 
  fickle.window_width - fickle.gutter - 244 - depth * (fickle.indent)

text_width = (pnt, depth) -> 
  Math.min .7 * (fickle.window_width - fickle.gutter - 144 ) - \
          depth * (fickle.indent), \
          fickle.max_text_width - depth * (fickle.indent)
  
opinion_width = (pnt, depth) -> 
  working_width(depth) - text_width(pnt, depth)


is_point_open = (pnt) -> 
  fetch('open_points')[(pnt.key or pnt)]?

toggle_point = (pnt, depth) -> 
  open = fetch('open_points')
  depth ||= 0 

  already_open = open[pnt.key]?

  if already_open
    delete open[pnt.key]
  else 
    for k,v of open 
      delete open[k] if k != 'key'

    # only enable parents to be open 
    open_pnt = pnt 
    while true 
      open[open_pnt.key] = depth
      depth -= 1

      if !open_pnt.parent || depth < 0
        break 

      open_pnt = fetch open_pnt.parent 

  save open

get_open_children = (pnt) -> 
  return [] if !pnt 
  open = fetch('open_points')
  pnt = fetch pnt 

  open_children = []
  for k,v of open 
    if k != 'key'
      ancestor = fetch k 
      while ancestor.parent && ancestor = fetch(ancestor.parent)
        if ancestor.key == pnt.key
          open_children.push k 
          continue 

  open_children 

close_children = (pnt) -> 
  open = fetch('open_points')

  if pnt 
    pnt = fetch pnt 
    open_children = get_open_children pnt 
  else 
    open_children = (k for k,v of open when k != 'key')

  if open_children.length > 0 
    for k in open_children
      delete open[k]
    save open

##############
# DATA ENTRY
#############

dom.NEW_POINT = -> 
  type = @props.type.key or @props.type
  you = your_key()
  w = 500

  depth = @props.depth

  DIV 
    style:
      position: 'relative'
      minHeight: 30
      marginTop: 10


    if !@local.editing
      SPAN 
        style: extend {}, list_styles.heading2,
          fontSize: 24 - 2 * depth
          #marginLeft: 68
          color: focus_blue
          cursor: 'pointer'
          borderBottom: "1px solid #{focus_blue}"

        onClick: (e) => 
          @local.editing = true; save(@local)
        
        "Add new"

    else 

      DIV 
        style: 
          marginLeft: -9
          display: 'inline-block'
          width: w
        TEXTAREA 
          key: 'summary'
          ref: 'summary'
          pattern:'^.{3,}'
          placeholder: "Summarize your point"
          required:'required'
          style: extend {}, list_styles.heading2,
            width: w
            border: "1px solid #eaeaea"
            outline: 'none'
            padding: '6px 8px'
            resize: 'none'

        GROWING_TEXTAREA
          key: 'description'
          ref: 'description'
          placeholder: "Add details here (optional)"

          style: 
            fontSize: 16
            padding: '6px 8px'
            marginTop: 8
            width: '100%'
            minHeight: 40
            border: '1px solid #eaeaea'   
            outline: 'none'       

        DIV 
          style: 
            marginTop: 8

          SPAN 
            style: 
              backgroundColor: focus_blue
              color: 'white'
              cursor: 'pointer'
              borderRadius: 16
              padding: '4px 24px'
              display: 'inline-block'
              marginLeft: 12
              float: 'right'

            onClick: => 
              create_point
                parent: @props.parent
                type: type
                summary: @refs.summary.getDOMNode().value
                description: @refs.description.getDOMNode().value 

              @local.editing = false 
              save @local

            'Done'

          SPAN 
            style: 
              color: '#888'
              cursor: 'pointer'
              float: 'right'
              padding: '4px 0px'
            onClick: => @local.editing = null; save(@local)
            'cancel'

          DIV style: clear: 'both'





######################
# Style
######################

focus_blue = '#2478CC'
feedback_orange = '#F19135'
attention_magenta = '#FF00A4'
logo_red = "#B03A44"
light_gray = '#afafaf'
light_blue = '#F6F7F9'

avenir = 'Avenir Next W01, Avenir Next, Avenir, Helvetica, sans-serif'
architect = 'Architects Daughter W00'

list_styles = 
  title: 
    fontWeight: 400
    fontSize: 42

  heading1: 
    color: focus_blue
    fontSize: 29
    fontWeight: 400
    fontFamily: architect

  heading2: 
    fontSize: 22
    fontWeight: 500


style = document.createElement "style"
style.innerHTML =   ""
document.body.appendChild style


#</script>

<script>
  var OFFLINE_DEV = true 
  var LOCAL_STATEBUS = true

  // The statebus server we will connect to. Read by statebus_client.
  if (window.location.hostname == 'localhost')
    statebus_server = 'http://localhost:9375'
  else 
    statebus_server = 'http://considerit.us:9375'
    
  
  if (LOCAL_STATEBUS)
    var statebus_client = "/considerit-local/node_modules/statebus/client.js"
  else 
    var statebus_client = "https://stateb.us/client.js?v3" 
    
  // External scripts to load
  var scripts_to_load = [ 
    statebus_client
  ]

  for (script in scripts_to_load)
    document.write("<script id=js-"+ script +  " src='" + scripts_to_load[script] + "' charset='utf-8'></script\>" )

</script>