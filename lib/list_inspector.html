<script type= "hot"> # -*- mode: coffee -*-


#######
# Considerit list inspector 
#
# Visualizes the list & point trees & their relationship.  

focus_blue = "rgb(36,120,204)"
transparent_focus_blue = (opacity) -> 
  "rgba(36, 120, 204, #{opacity})"
feedback_orange = '#F19135'
logo_red = "rgb(176,58,68)"
light_gray = '#999'
attention_magenta = '#FF00A4'


link_types = ['parent/child', 'is_a', 'suggests']

cntrl = 
  key: 'controls'
  removed_nodes: []
  show: {}

for t in link_types
  cntrl.show[t] = t != 'parent/child'

save cntrl

save
  key: 'root'
  point: "/point/bitcoinclassicv2"

save 
  key: 'network'
  nodes: {}
  links: {}



# Make the network

# build & maintain the network of points and lists
window.DataLiaison = statebus.reactive -> 
  root_pnt = fetch(fetch("root").point)

  # make sure everything is loaded
  visit_connected root_pnt
  return if DataLiaison.loading()

  network = fetch 'network'

  nodes = {}
  links = {}

  #################
  # CREATE NODES
  # 
  # Make a node for each point & list

  for own k,v of statebus.cache
    key = "_#{k}"

    if k.substring(0, 7) == '/point/'
      is_root = key == "_" + root_pnt.key
      nodes[key] =
        name: k
        prefix: k.split('/').filter( (f) -> f != '')[0]

        has_children: v.children?.length > 0
        has_type_children: v.type_children?.length > 0
        is_suggested_by: v.suggested_by?.length > 0

        graph_key: key
        fixed: nodes[key]?.fixed or is_root
        is_root: is_root
        connections: 0


  #################
  # CREATE LINKS
  # 
  # walk the point and list trees to add links
  
  add_link = (obj1, obj2, type) -> 
    obj1_key = "_#{obj1.key or obj1}"
    obj2_key = "_#{obj2.key or obj2}"
    if (obj1_key of nodes) && (obj2_key of nodes)
      nodes[obj1_key].connections += 1
      nodes[obj2_key].connections += 1
      links["#{obj1_key},#{obj2_key}"] = type

  visited = {}

  max_depth = 0

  visit_point = (pnt, depth) -> 
    return if visited[(pnt.key or pnt)]
    visited[(pnt.key or pnt)] = true

    pnt = fetch pnt 
    n = nodes["_" + pnt.key]
    n.depth = depth 
    if max_depth < depth 
      max_depth = depth

    # connect to parent 
    if pnt.parent 
      add_link(pnt, pnt.parent, 'parent/child')

    # connect to type
    if pnt.type 
      add_link(pnt, pnt.type, 'is_a')
      visit_point pnt.type, depth - 1

    if pnt.suggests 
      for suggestion in pnt.suggests
        add_link(pnt, suggestion, 'suggests')
        visit_point suggestion, depth + 1

    if pnt.suggested_by?.length > 0
      for offerer in pnt.suggested_by
        add_link(offerer, pnt, 'suggests')
        visit_point offerer, depth - 1

    for subpnt in (pnt.children or [])
      visit_point subpnt, depth + 2

    for instance in (pnt.type_children or [])
      visit_point instance, depth + 2


  visit_point fetch(fetch("root").point), 0 

  # let's try to see if any unlinked nodes have actually been deleted 
  # on a different client
  ls = {}
  for own k,v of links
    [k1,k2] = k.split(',')
    ls[k1] = 1
    ls[k2] = 1

  for own k,v of nodes 
    if !ls[k] && !v.is_root
      statebus.forget k.substring(1), DataLiaison.bus
      delete nodes[k]
      fetch k.substring(1)

  if JSON.stringify(nodes) != JSON.stringify(network.nodes) || \
     JSON.stringify(links) != JSON.stringify(network.links)
    network.nodes = nodes
    network.links = links 
    network.max_depth = max_depth
    save network 

# start monitoring the data
DataLiaison()


####
# d3Data
#
# Takes our data network and transforms them into d3-consumable
# data, while also accommodating the UI controls

deep_clone = (obj) -> 
  JSON.parse JSON.stringify(obj)

d3Data = statebus.reactive -> 

  controls = fetch 'controls'
  network = fetch 'network' # statebus points/lists
  d3_network = fetch 'd3_network' # transformed d3 network of points/lists

  return if DataLiaison.loading()

  links = network.links
  nodes = {}
  for key, node of network.nodes
    # Prefer to use the d3-enabled node form d3_network, if it exists.
    # This ensures stability in the network visualization as the state changes.  
    n = null 
    for d3_node in (d3_network.nodes or [])
      if d3_node.graph_key == key 
        n = d3_node
        break
    nodes[key] = n or node

  d3_links = []

  link_enabled = (n1, n2) -> 
    link_type = links["#{n1},#{n2}"] or links["#{n2},#{n1}"]
    present = !!link_type
    active = !(n1 in controls.removed_nodes) && !(n2 in controls.removed_nodes)
    enabled = controls.show[link_type]
    present && active && enabled

  # Prepare link weights to find orphaned nodes
  weights = {}
  for own k,v of links
    [k1,k2] = k.split(',')

    if link_enabled k1, k2
      weights[k1] = 1 + (weights[k1] or 0)
      weights[k2] = 1 + (weights[k2] or 0)

      d3_links.push 
        source: nodes[k1]
        target: nodes[k2]
        value: v
        type: v

  d3_nodes = (v for k,v of nodes when k of weights or true)

  if JSON.stringify(d3_nodes) != JSON.stringify(d3_network.nodes) || \
     JSON.stringify(d3_links) != JSON.stringify(d3_network.links)
    d3_network.max_depth = network.max_depth
    d3_network.nodes = d3_nodes
    d3_network.links = d3_links 
    save d3_network 

d3Data()


# Visualize



body = -> 
  DIV 
    style:
      backgroundColor: 'white'
      zIndex: 9999999
      padding: '20px 5px 20px 20px'

    Controls()

    NodeDetails()

    NetworkViz()

    Footer()

    LINK 
      href: 'http://fonts.googleapis.com/css?family=Orbitron:400,500,700' 
      rel: 'stylesheet' 
      type: 'text/css'

    STYLE dangerouslySetInnerHTML:
      __html: """
        .tooltip { 
          font-family: Orbitron, sans-serif;
          color: white;
          font-weight: 400;
          font-size: 24px;
          background-color: #{light_gray};
        }
        .tooltip.list { 
          background-color: #{feedback_orange};
        }
      """



controls = -> 
  network = fetch 'network'
  controls = fetch 'controls'


  # header area
  DIV 
    style: 
      position: 'relative'
      minWidth: 1000

    # banner
    SPAN
      style: 
        fontSize: 65
        color: 'white'
        # WebkitTextFillColor: 'white'
        # WebkitTextStrokeWidth: 1
        # WebkitTextStrokeColor: 'black'
        fontFamily: 'Orbitron, sans-serif'
        backgroundColor: logo_red

      'Considerit inspector'

    SPAN 
      style: 
        marginLeft: 20

      INPUT 
        type: 'text'
        placeholder: 'Root point key'
        defaultValue: fetch('root').point 
        ref: 'root_point'
        style: 
          padding: '6px 12px'
          fontSize: 24
        onChange: =>   
          root = fetch 'root'
          root.point = @refs.root_point.getDOMNode().value
          save root 



    # controls
    DIV 
      style: 
        margin: '25px 0'

      for prop in link_types
        active = controls.show[prop]
        do (prop,active) ->
          SPAN 
            style: 
              fontSize: 24
              fontFamily: 'Orbitron, sans-serif'
              color: if active then attention_magenta else '#aaa'
              cursor: 'pointer'
              marginRight: 30
              display: 'inline-block'

            onClick: -> 
              controls.show[prop] = !controls.show[prop]
              save controls

            prop.replace /\_/g, ' '

      A
        style: 
          textDecoration: 'underline'
          margin: '0 10px'
          fontSize: 24
          fontFamily: 'Orbitron, sans-serif'
          cursor: 'pointer'
          color: '#777'

        onClick : (e) => 
          e.stopPropagation()
          controls.removed_nodes = []
          network = fetch 'd3_network'
          for n in network.nodes
            n.fixed = n.has_focus = false 
          save controls
          save network
        'reset'

footer = -> 
  network = fetch 'network'
  controls = fetch 'controls'

  # For displaying a counter...
  num_types = (n for own k,n of network.nodes when (n.has_type_children || n.is_suggested_by)).length.toString()
  num_points = (n for own k,n of network.nodes when !n.is_suggested_by).length.toString()
  # num_forks = (l for own k,l of network.links when l == 'forked').length.toString()

  pad = (num, len) -> 
    str = num.toString()
    i = 0 
    while i < len - num.toString().length
      str = "0" + str
      i += 1
    str

  max_digits = Math.max num_types.length, \
                        num_points.length

  counter = [ [pad(num_types,   max_digits), 'types'], \
              [pad(num_points, max_digits), 'points'] ]

  instructions = ['Drag/click any node to fix its position', \
                  'Shift+click a fixed node to free it', \
                  'Click a data node to see its values', \
                  'Alt+click a node to remove it']

  # counter + instructions
  DIV 
    style: 
      marginTop: 5
      minWidth: 1000

    DIV 
      style: 
        display: 'inline-block'

      for [num, label] in counter
        DIV 
          style: 
            marginBottom: 15

          for digit in num
            SPAN
              style: 
                border: '1px solid #f4f4f4'
                boxShadow: '0 1px 1px black'
                paddingTop: 7
                fontWeight: 600
                fontFamily: 'Orbitron, sans-serif'
                fontSize: 30
                marginRight: 8
                width: 36
                display: 'inline-block'
                textAlign: 'center'
              digit
          SPAN
            style: 
              marginLeft: 10
              fontSize: 24
              marginTop: 10
              fontFamily: 'Avenir' #'Orbitron, sans-serif'
            label

    DIV           
      style: 
        fontSize: 24
        color: 'black'
        marginLeft: 150
        display: 'inline-block'
        padding: '8px 0'
        fontFamily: 'Avenir' #'Orbitron, sans-serif'
        fontSize: 24
        verticalAlign: 'top'

      for inst in instructions

        DIV
          style: 
            marginBottom: 5

          inst


node_details = -> 
  # data for a focus node
  controls = fetch 'controls'
  network = fetch 'd3_network'

  return SPAN null if d3Data.loading() || DataLiaison.loading()

  focus = null 
  for n in network.nodes 
    if n.has_focus
      focus = n
      break 

  return SPAN null if !focus 

  DIV 
    style: 
      position: 'relative'

    DIV 
      style:
        position: 'absolute'
        left: GRAPH_WIDTH() - 240
        top: 10
        backgroundColor: '#eaeaea'
        overflow: 'hidden'
        zIndex: 1

      for own k,v of fetch(focus.name)
        text = pretty_print v
        DIV
          style: 
            maxWidth: 230
            backgroundColor: 'white'
            marginBottom: 10

          DIV 
            style: 
              fontFamily: 'Orbitron, sans-serif'                          
            k
          DIV 
            style: 
              paddingLeft: 10
              fontSize: 12

            for para,idx in text.split('\n')
              [if idx > 0
                BR null
              SPAN null, para]

network_viz = -> 

  d3_network = fetch 'd3_network'
  controls = fetch 'controls'

  return SPAN null if !d3_network.max_depth?
  @level_height = GRAPH_HEIGHT() / (d3_network.max_depth + 1)

  show_levels = false 
  d = 0

  DIV 
    style: 
      width: GRAPH_WIDTH() + 4 + if show_levels then 10 else 0

    DIV 
      style: 
        width: 10
        height: GRAPH_HEIGHT()
        display: 'inline-block'
        display: if !show_levels then 'none'

      while d < d3_network.max_depth + 1
        d++ 
        DIV
          style: 
            backgroundColor: if d % 2 then focus_blue else logo_red
            height: @level_height

    DIV 
      id: 'graph'
      ref: 'graph' 
      style: 
        display: 'inline-block'


network_viz.refresh = network_viz.up = ->  
  d3_network = fetch 'd3_network'

  return if d3Data.loading() || DataLiaison.loading()

  d3_nodes = d3_network.nodes 
  d3_links = d3_network.links 
  
  width = GRAPH_WIDTH()
  height = GRAPH_HEIGHT()

  for n in d3_nodes
    n.y ||= (n.depth + .5) * @level_height
    n.x ||= width / 2

  if !@force 
    @force = d3
      .layout
      .force()
      .charge (d) -> 
        if d.has_type_children || d.is_suggested_by
          -400
        else 
          -100
      #.chargeDistance 400
      .gravity 0  # every node is attracted instead to a depth
      .friction .5
      .linkStrength (l) -> 1
      .linkDistance (l) => 
        d = nodeSize(l.source) + nodeSize(l.target)
        if !l.source.fixed && !l.target.fixed           
          d += Math.abs(l.target.depth - l.source.depth) * @level_height / 2

        if l.type in ['suggests']
          d *= 2
        d 

    svg = d3
      .select '#graph'
      .append 'svg'

    # Tooltip for hovering over a node
    @tip = d3.tip()
      .attr 'class', 'd3-tip'
      .offset [-10, 0]
      .html (d) -> 
        "<div class='tooltip #{if d.has_type_children || d.is_suggested_by then 'list' else 'point'}'>" + \
        "#{d.name}" + "</div>"
    svg.call @tip
  else 
    svg = d3.select '#graph svg'

  svg
    .attr("width", width)
    .attr("height", height)
  

  svg.append("svg:defs")
    .selectAll("marker")
      .data d3_links, (l) -> "#{l.source.graph_key},#{l.target.graph_key}"
    .enter().append "svg:marker"
      .attr "id", (l) -> "marker-#{l.source.graph_key}-#{l.target.graph_key}"
      .attr "viewBox", "0 -5 10 10"
      .attr "refX", (l) -> 10 + nodeSize l.target
      .attr "refY", -1.5
      .attr "markerWidth", 6
      .attr "markerHeight", 6
      .attr "orient", "auto"
    .append("svg:path")
      .attr "d", "M 0,-5 L 10,0 L 0,5"
      .attr "fill", (l) -> 
        if l.type == 'suggests'
          feedback_orange
        else if l.type == 'is_a'
          light_gray
        else 
          'black'

  @force
    .nodes d3_nodes
    .links d3_links
    .start()


  link = svg.selectAll('.link')
      .data d3_links, (l) -> "#{l.source.graph_key},#{l.target.graph_key}"

  link
    .enter().append("svg:path")
      .attr 'class', (l) -> "link #{l.type.replace(/\//g, '_')}"
      .attr "marker-end", (l) -> 
        if l.type in ['suggests']
          "url('#marker-#{l.source.graph_key}-#{l.target.graph_key}')"

  link.exit().remove()

  link
    .style 'fill', 'none'
    .style 'stroke-dasharray', (l) -> 
      if l.type in ['is_a', 'parent/child']
        "1,1"
      else 
        "0"
    .style 'stroke-width', (l) -> 
      if l.type in ['is_a', 'parent/child']
        2
      else 
        1
    .style 'stroke', (l) -> 
      if l.type == 'suggests'
        feedback_orange

      else if l.source.has_focus || l.target.has_focus
        attention_magenta
      else 
        "rgb(240,240,240)"

  drag = @force
    .drag()
    .on "dragstart", (pnt) -> pnt.fixed = true 

  node = svg
    .selectAll '.node'
    .data d3_nodes, (d) -> d.graph_key

  node
    .enter()
    .append 'rect'
    .attr 'class', (d) -> if d.has_type_children || d.is_suggested_by then 'node list' else 'node point'
    .style 'cursor', 'pointer'
    .on 'mouseover', (d) => @tip.show d
    .on 'mouseout', (d) => @tip.hide d
    .on 'click', (d) -> 

      if d3.event.defaultPrevented
        return # when dragged

      controls = fetch 'controls'

      if d3.event.altKey
        controls.removed_nodes.push d.graph_key
      else if d3.event.shiftKey && d.fixed
        controls = fetch 'controls'
        d.fixed = d.has_focus = false        
        controls.focus = false 
      else
        controls = fetch 'controls'
        if d.has_focus
          d.has_focus = false 
        else 
          for n in d3_nodes 
            n.has_focus = false
          d.has_focus = true 
        controls.focus = false 

      save controls

    .call drag

  node
    .exit()
    .remove()

  node
    .attr 'width', (d) -> 2 * nodeSize d
    .attr 'height', (d) -> 2 * nodeSize d
    .attr 'rx', (d) -> nodeSize(d)
    .attr 'ry', (d) -> nodeSize(d)
    .style 'fill', (d) -> 
      if d.has_type_children || d.is_suggested_by
        feedback_orange
      else
        light_gray

    .style 'opacity', (d) -> 
      if d.weight == 0 
        '.2'
      else 
        '1.0'
    .style 'stroke', (d) -> 
      if d.has_focus
        attention_magenta
      else 
        'white'
    .style 'stroke-width', (d) -> 
      if d.has_focus
        4
      else 
        1

  # Sort because z-index doesn't work on SVG elements.
  # We want all links below nodes, and big nodes below
  # small nodes. 
  svg.selectAll('.node, .link').sort (a,b) -> 
    if a.target? && b.target?
      0
    else if a.target?
      -1
    else if b.target
      1
    else
      b.weight - a.weight


  @force.on 'tick', (e) =>
    q = d3.geom.quadtree d3_nodes
    for d in d3_nodes

      # handle collisions
      q.visit collide(d)

      s = nodeSize(d)

      # push toward depth
      if !d.fixed 
        target_range = [ d.depth * @level_height + 10 + s/2, \
                        (d.depth + 1) * @level_height - s/2]

        y_attractor = (target_range[0] + target_range[1]) / 2
        dist = Math.abs(y_attractor - d.y)
        force = e.alpha * Math.pow(dist / @level_height, 1.5)

        d.y += (y_attractor - d.y) * Math.min(1, force)



      # stay within bounds
      if d.x < s
        d.x = s
      if d.x > width - s
        d.x = width - s

      if d.y < s
        d.y = s
      if d.y > height - s
        d.y = height - s

    link
      .attr 'd', (l) -> 
        if l.type in ['suggests']
          # curved line
          dx = l.target.x - l.source.x
          dy = l.target.y - l.source.y
          rx = ry = Math.sqrt(dx * dx + dy * dy)

          """M#{l.source.x},#{l.source.y} 
             A#{rx},#{ry} 0,0,1
             #{l.target.x},#{l.target.y}"""
        else 
          "M#{l.source.x},#{l.source.y} #{l.target.x},#{l.target.y}"

    node
      .attr 'x', (d) -> d.x - nodeSize(d)
      .attr 'y', (d) -> d.y - nodeSize(d)







#######################
# Component registry
#######################
views = ['body', 'controls', 'network_viz', 'node_details', 'footer']


visit_connected = (pnt) -> 
  pnt = fetch pnt
  visit_ancestors pnt
  visit_descendents pnt
    
visit_ancestors = (pnt) -> 
  pnt = fetch pnt
  ancestor = pnt
  while ancestor.parent
    ancestor = fetch ancestor.parent

  ancestor = pnt 
  while ancestor.type 
    ancestor = fetch ancestor.type

visit_descendents = (pnt) -> 
  pnt = fetch pnt
  
  if pnt.parent
    fetch pnt.parent
    
  if pnt.type 
    fetch pnt.type
    
  for subpnt in (pnt.children or [])
    visit_descendents subpnt

  for instance in (pnt.type_children or [])
    visit_descendents instance





reactive_once = (func) -> 
  f = statebus.reactive -> 
    func()
    if !f.loading()
      f.forget_everything()
  f 









nodeSize = (node) -> 
  if (!(node.has_type_children || node.is_suggested_by) && node.connections < 3) || node.connections < 2
    5
  else 
    10 #  Math.sqrt(4 * node.weight) + 3

collide = (node) -> 
  (quad, x1, y1, x2, y2) -> 
    if quad.leaf && quad.point && quad.point != node
      dx = node.x - quad.point.x
      dy = node.y - quad.point.y
      dist = Math.sqrt(dx * dx + dy * dy)
      combined_r = nodeSize(node) + nodeSize(quad.point)

      # repel both points equally in opposite directions if they overlap
      if (dist < combined_r) 
        separate_by = if dist == 0 then 1 else ( dist - combined_r ) / dist
        offset_x = dx * separate_by * .6
        offset_y = dy * separate_by * .6

        node.x -= offset_x
        node.y -= offset_y
        quad.point.x += offset_x
        quad.point.y += offset_y
      
    neighborhood_radius = nodeSize(node) + 16
    nx1 = node.x - neighborhood_radius
    nx2 = node.x + neighborhood_radius
    ny1 = node.y - neighborhood_radius
    ny2 = node.y + neighborhood_radius

    return x1 > nx2 || 
            x2 < nx1 ||
            y1 > ny2 ||
            y2 < ny1

window.extend = (obj) ->
  obj ||= {}
  for arg, idx in arguments 
    if idx > 0
      for own name,s of arg
        if !obj[name]? || obj[name] != s
          obj[name] = s
  obj

window.defaults = (obj) ->
  obj ||= {}
  for arg, idx in arguments by -1
    if idx > 0
      for own name,s of arg
        if !obj[name]?
          obj[name] = s
  obj

################
# SVG helpers


# fixed saturation & brightness; random hue
# adapted from http://martin.ankerl.com/2009/12/09/how-to-create-random-colors-programmatically/
golden_ratio_conjugate = 0.618033988749895

window.getNiceRandomHues = (num, seed) -> 
  h = seed or .5

  hues = []
  i = num
  while i > 0
    hues.push h % 1
    h += golden_ratio_conjugate
    i -= 1
  hues

window.hsv2rgb = (h,s,v) -> 
  h_i = Math.floor(h*6)
  f = h*6 - h_i
  p = v * (1 - s)
  q = v * (1 - f*s)
  t = v * (1 - (1 - f) * s)
  [r, g, b] = [v, t, p] if h_i==0
  [r, g, b] = [q, v, p] if h_i==1
  [r, g, b] = [p, v, t] if h_i==2
  [r, g, b] = [p, q, v] if h_i==3
  [r, g, b] = [t, p, v] if h_i==4
  [r, g, b] = [v, p, q] if h_i==5

  "rgb(#{Math.round(r*256)}, #{Math.round(g*256)}, #{Math.round(b*256)})"


# Utilities

xor = (a,b) -> ( a || b ) && !( a && b )

pretty_print = (obj) -> 
  result = ""
  if Array.isArray(obj)
    if obj.length == 0
      result = "[]"
    else
      result += "[\n"
      for k,idx in obj
        result += "#{pretty_print(k)}"
        result += ', ' if idx != obj.length - 1
      result += "\n]"
  else if obj == null
    result += 'null'
  else if typeof obj == 'object'
    result += "{\n"
    for own k,v of obj
      result += "#{k}: #{pretty_print(v)},"

    result += "\n}\n"
  else if typeof obj == 'string'
    if obj == ''
      result += '""'
    else
      result += obj
  else if typeof obj == 'function'
    result += '[function]'
  else
    result += JSON.stringify(obj)

  return result

######
# set_responsive
#
# Updates the responsive variables. Called once on system load, and then subsequently
# whenever there is a relevant system event that would demand the variables to be 
# recalculated (e.g. window resizing)
set_responsive = -> 
  responsive = fetch('responsive_vars')

  w = window.innerWidth
  h = window.innerHeight

  new_vals = 
    WINDOW_WIDTH: w
    WINDOW_HEIGHT: h
    GRAPH_HEIGHT: 700
    GRAPH_WIDTH: w - 50

  # only update if we have a change
  has_new = false
  for own k,v of new_vals
    if responsive[k] != v
      has_new = true
      responsive[k] = v

  save(responsive) if has_new
      
# Initialize the responsive variables
set_responsive()

# Whenever the window resizes, we need to recalculate the variables.
window.addEventListener "resize", set_responsive

# Convenience method for programmers to access responsive variables.
responsive = fetch('responsive_vars')
for lvar in Object.keys(responsive)
  do (lvar) ->
    window[lvar] = -> fetch('responsive_vars')[lvar]


load_js = (js) -> 
  document.write('<script src="' + js + '"></' + 'script>')


external_scripts = [
  'vendor/d3.v3.min.js',
  'vendor/d3.tip.js',
  'vendor/md5.js'
]
for script in external_scripts  
  load_js(script)

window.sbsio_msgs = false

#</script>
<script>
  minify = true
  dropbox = "./"
  statebus_version = 2
  statebus_server = "socketios://consider.it:3002"
</script>
<script src="vendor/singlefile.js"></script>
<!--<script src="vendor/singlefile.js"></script>-->



